@ECHO OFF

REM BUILD EMPTY ROOM

set "x1=SET MID=%%MID%%%%MID%%%%MID%%"
SET MID=....
for /l %%A in (1,1,4) do call %x1%

SET MID=%mid:~0,118%
SET MID=#%MID%#
SET TOP=%MID:.=#%

for /l %%A in (1,1,3) do call %x1%

SET EMPTY=%TOP%%MID%%TOP%

REM EMPTY ROOM AND FILL IT

SET "ROOM=%EMPTY%"

CALL :INSERTER $ 30
FOR %%A IN (1700 1581 1462 1343 1224 1821 1702 1583 1464 1345 1942 1823  1704  1585  1466 2063 1944 1825  1587 1706 2184 2065 1946 1827 1708) do (CALL :INSERTER X 1 %%A)

FOR %%A IN (1701 1582 1463 1344 1822 1703 1584 1465 1943 1833 1824 1705 1586 2064 1945 1826 1707) do (CALL :INSERTER X 1 %%A)
set "POS=121"

REM MAIN GAME LOOP

:START
SET /A POS2=%POS%+1

REM DRAW ROOM AND HUD

echo.
CALL <nul set /p =%%room:~0,%POS%%%
<nul set /p =@
CALL <nul set /p =%%room:~%POS2%%%
<nul set /p =POS:%POS%  GOLD:%GOLD%

REM GET INPUT AND CALCULATE RESULTS

CHOICE /C WASD > NUL
CALL :SPIRAL %ERRORLEVEL%
SET /A NEWPOS=%POS%+%ERRORLEVEL%
CALL :PICKER %NEWPOS% ITEM
IF [%ITEM%] EQU [$] SET /A "GOLD+=1"
IF [%ITEM%] NEQ [#] (CALL :INSERTER ~ 1 %POS% & SET "POS=%NEWPOS%")
GOTO START

:PICKER
CALL set "%2=%%ROOM:~%1,1%%" > NUL
EXIT /B

:INSERTER
SET /A A=%A%+1
:TRYAGAIN
Set /a rand=(%RANDOM%*3480/32768)+1 
IF [%3] NEQ [] SET RAND=%3
set /a rand2=%rand%+1
CALL :PICKER %RAND% ITEM
IF [%3] EQU [] IF [%ITEM%] NEQ [.] IF [%ITEM%] NEQ [~] GOTO TRYAGAIN
CALL set "room=%%room:~0,%RAND%%%%%1%%room:~%RAND2%%%" > NUL
IF [%A%] NEQ [%2] GOTO INSERTER
SET A=0
EXIT /B

:SPIRAL
set "z=%1"
set /a y=((%z%%%2 * 119) + 1) * ((%z%/3)*2-1)
EXIT /B %y%